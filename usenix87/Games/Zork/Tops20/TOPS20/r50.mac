	TITLE R50
	ENTRY R50CNV,G50

P=17
AP=16
CHR=1
WRD=2
PNT=3
BASE=4

R50CNV:	ADD	P,[4,,4]
	DMOVEM	1,-3(P)
	DMOVEM	3,-1(P)
	MOVN	BASE,@1(AP)	;NEG COUNT OF WORDS
	MOVSS	BASE
	HRR	BASE,0(AP)	;ARRAY BASE
CLUP:	MOVE	PNT,[POINT 7,0(BASE)]
	MOVE	WRD,0(BASE)	;PICK UP WORD
	CAME	WRD,[-1]
	TLNN	WRD,774000	;HAVE A CHAR?
	JRST	CNXT		;NO - DO NEXT
	PUSHJ	P,CNV50		;GET CHAR AND CONVERT
	MOVE	WRD,CHR
	IMULI	WRD,50
	PUSHJ	P,CNV50
	ADD	WRD,CHR
	IMULI	WRD,50
	PUSHJ	P,CNV50
	ADD	WRD,CHR
	MOVEM	WRD,0(BASE)	;PUT WORD BACK
CNXT:	AOBJN	BASE,CLUP
	DMOVE	1,-3(P)
	DMOVE	3,-1(P)
	SUB	P,[4,,4]
	POPJ	P,

G50:	PUSH	P,CHR
	LDB	CHR,[POINT 7,@0(AP),6]
	PUSHJ	P,CNV50Z
	MOVE	0,CHR
	POP	P,CHR
	POPJ	P,
CNV50:	ILDB	CHR,PNT		;GET NEXT CHAR
CNV50Z:	CAIL	CHR,"A"
	CAILE	CHR,"Z"
	JRST	CNV50A
	SUBI	CHR,100
	POPJ	P,

CNV50A:	CAIL	CHR,"0"
	CAILE	CHR,"9"
	JRST	CNV50B
	SUBI	CHR,22
	POPJ	P,

CNV50B:	CAIL	CHR,"A"+40
	CAILE	CHR,"Z"+40
	JRST	CNV50C
	SUBI	CHR,140
	POPJ	P,

CNV50C:	CAIE	CHR,"-"
	JRST	CNV50D
	SUBI	CHR,22
	POPJ	P,

CNV50D:	MOVEI	CHR,0
	POPJ	P,

	END
