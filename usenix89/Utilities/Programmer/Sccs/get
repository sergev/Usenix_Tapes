#!/bin/csh -f
# Shell script to check out SCCS files
#
set edit rev pflg
while ( $#argv )
    switch ( $argv[1] )
        case "-e":
            set edit = "-e "
            breaksw
        case "-a":
            set getall
            breaksw
        case "-r":
            shift
            set rev = "-r$argv[1] "
            breaksw
        case "-r[1-9]*":
            set rev = "$argv[1] "
            breaksw
        case "-p"
            set pflg = "-p "
            breaksw
        case "-*"
            echo Unknown option \"$argv[1]\" ignored.
            breaksw
        default:
            if ( ! $?args ) set args
            set args = ( $args S/s.$argv[1] )
            set getall
            breaksw
    endsw
    shift
end
if ( ! -e S ) then
    echo -n "No S directory."
    goto quit
endif
if ( ! $?args ) set args = S/s.*
if ( $#args == 0 ) then
    echo "No files."
    goto quit
endif
foreach i ( $args )
    if ( ! $?getall ) then
        echo -n "Get $pflg$edit$rev`basename $i` (y/n)? "
        set ans = $<
        if ( $ans == "y" ) /usr/bin/get -s $pflg$edit $rev $i
    else
        echo $pflg$edit$rev $i
        /usr/bin/get -s $pflg$edit $rev $i
    endif
end
quit:
